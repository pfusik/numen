	icl	'../nex.asx'

Pic_BAK	equ	0

Pic_dl1	equ	$f000
Pic_dl2	equ	$f400

Pic_p1	equ	$80
Pic_p2	equ	$82
Pic_pB	equ	$84

	org	$4010
Pic_gr9	ins	'ejn.gr9'
	ert	*!=Pic_gr9+40*118	; must be 118 lines!!!

	org	$5310
Pic_grB1	ins	'ejn.grB',0,40*38

	org	$6010
Pic_grA	ins	'ejn.grA'

	org	$7300
Pic_grB2	ins	'ejn.grB',40*38

	ift	NUMEN
main	equ	NextBlock
	org	$c600
	lda:rne	^4b
	mwa	#rts	Vbl_vector
	els
main	sei
	inc	^4e
	lda:rne	^4b
	ldx	#29
	sta:rpl	^00,x-
	eif

	mva	#$90	Pic_dl1
	sta	Pic_dl2
	mva	>Pic_dl1	Pic_p1+1
	mva	>Pic_dl2	Pic_p2+1
	mwa	#$10	Pic_pB
	lda	<Pic_dl1+1
	ldx	#118

Pic_mdl	sta	Pic_p1
	sta	Pic_p2
	mva	#$4f	(Pic_p1),0
	sta	(Pic_p2),y
	ldy	#3
	sta	(Pic_p1),y
	sta	(Pic_p2),y
	ldy	#1
	mva	Pic_pB	(Pic_p1),y
	sta	(Pic_p2),y
	ldy	#4
	sta	(Pic_p1),y
	sta	(Pic_p2),y
	ldy	#2
	lda	Pic_pB+1
	cmp	#6
	scc
	adc	#>Pic_grB2->Pic_grB1-6-1	+
	adc	>Pic_grB1	-
	sta	(Pic_p1),y
	sta	(Pic_p2),y
	ldy	#5
	txa
	lsr	@
	lda	Pic_pB+1
	eor	>Pic_gr9
	scc:eor	>Pic_gr9^Pic_grA
	sta	(Pic_p1),y
	eor	>Pic_gr9^Pic_grA
	sta	(Pic_p2),y
	lda	#40
	add:sta	Pic_pB
	scc:inc	Pic_pB+1
	lda	#6
	add	Pic_p1
	bcc	Pic_skp
	inc	Pic_p1+1
	inc	Pic_p2+1
Pic_skp	dex
	bne	Pic_mdl

	mva	#$41	(Pic_p1),0
	sta	(Pic_p2),y+
	mva	<Pic_dl1	(Pic_p1),y
	sta	(Pic_p2),y+
	mva	>Pic_dl2	(Pic_p1),y
	mva	>Pic_dl1	(Pic_p2),y

	lda:rne	^4b
	mva	#$22	^40
	mwa	#Pic_dl1	^42
	ldx	#8
	mva:rpl	Pic_cols,x	^12,x-

	ift	NUMEN
	mwa	#dli	Dli_vector
	mwa	#-282	Vbl_clock
	jsr	Idle
	jmp	NextBlock
	els
	mwa	#dli	$fffa
	mva	#$80	^4e
	jmp	*
	eif

dli
:!NUMEN	pha
:NUMEN	lda:pha	^31
:NUMEN	mva	Banks_table+Banks_MAIN_INDEX	^31
	txa:pha
	tya:pha
	ldx	#$c0
	ldy	#118
	lda	#$40
dli_gtiaMode	equ	*-1
dli_1
	sta	^4a
	stx	^1b
	sta	^4a
	sta	^1b
	eor	#$40^$80
	dey
	bne	dli_1
	eor	#$40^$80
	sta	dli_gtiaMode
	pla:tay
	pla:tax
:NUMEN	pla:sta	^4a
:NUMEN	sta	^31
	pla
	rti

Pic_cols
	dta	Pic_BAK,Pic_BAK+2,Pic_BAK+4,Pic_BAK+6,Pic_BAK+8,Pic_BAK+10,Pic_BAK+12,Pic_BAK+14,Pic_BAK

	end
